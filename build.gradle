plugins {
  id 'java-library'
  id 'project-setup' apply false
  alias libs.plugins.cadixdev.licenser
  alias libs.plugins.springframework.boot apply false
  alias libs.plugins.springframework.depManagement apply false
}

group = 'com.github.tmslpm.hsla'
version = '1.0.0'

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'project-setup'
  apply plugin: libs.plugins.springframework.boot.get().pluginId
  apply plugin: libs.plugins.springframework.depManagement.get().pluginId
  apply plugin: libs.plugins.cadixdev.licenser.get().pluginId

  group = "${rootProject.group}.$name"
  version = rootProject.version

  base {
    archivesName = "$rootProject.name-$name"
  }

  dependencies {
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter'
    switch (name) {
      case 'app':
        implementation project(":src:pl")
        break
        //   A P P   //
        //  ---  ▲   //
        //   │   │   //
        //   ▼  ---  //
      case 'pl':
        api project(":src:bll")
        testImplementation project(":src:app")
        break
        //  ---  ▲   //
        //   │   │   //
        //   ▼  ---  //
      case 'bll':
        api project(":src:dal")
        testImplementation project(":src:app")
        break
        //  ---  ▲   //
        //   │   │   //
        //   ▼  ---  //
      case 'dal':
        testImplementation project(":src:app")
        break
      default: break
    }
  }

  license {
    header = project.rootProject.file('license.md')
  }

}
